# Copyright (C) 2013 Ion Torrent Systems, Inc. All Rights Reserved

option(ION_USE_MKL "Using Intel MKL" OFF)
mark_as_advanced(ION_USE_MKL)

option(ION_USE_SYSTEM_CUDA "Use CUDA system libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_CUDA)

option(ION_USE_SYSTEM_SAMTOOLS "Use samtools system libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_SAMTOOLS)

option(ION_USE_SYSTEM_BAMTOOLS "Use bamtools system libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_BAMTOOLS)

option(ION_USE_SYSTEM_FFTW "Use fftw system libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_FFTW)

option(ION_USE_SYSTEM_HDF5 "Use hdf5 system libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_HDF5)

option(ION_USE_SYSTEM_ARMADILLO "Use armadillo system libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_ARMADILLO)

option(ION_USE_SYSTEM_BOOST "Use boost system libraries" ON)
mark_as_advanced(ION_USE_SYSTEM_BOOST)

option(ION_USE_SYSTEM_UNWIND "Use unwind system libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_UNWIND)

option(ION_USE_SYSTEM_LEVMAR "Use systems default levmar libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_LEVMAR)

option(ION_USE_SYSTEM_PICARD "Use picard system libraries" OFF)
mark_as_advanced(ION_USE_SYSTEM_PICARD)


# wget http://developer.download.nvidia.com/compute/cuda/4_2/rel/toolkit/cudatoolkit_4.2.9_linux_64_ubuntu11.04.run
# MYCUDASDK=cudatoolkit_4.2.9_linux_64_ubuntu11.04
# bash $MYCUDASDK.run --nox11 --noexec --target $MYCUDASDK
# GZIP=--best tar czf $MYCUDASDK.tar.gz $MYCUDASDK
ExternalProject_add(${cuda_toolkit}
    PREFIX ${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}-prefix
    SOURCE_DIR ${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}
    URL "http://${ION_UPDATE_SERVER}/updates/software/external/${cuda_toolkit_tar_file}"
#    URL_MD5 ""
    TIMEOUT "3600"
    CONFIGURE_COMMAND ""
    BUILD_COMMAND ""
    INSTALL_COMMAND ""
    PATCH_COMMAND patch -p1 -t -N < "${PROJECT_SOURCE_DIR}/../external/cuda_toolkit-5.0.35.patch" 
)


ExternalProject_add(${cuda_devdriver}
    PREFIX ${PROJECT_BINARY_DIR}/../${cuda_devdriver_version}-prefix
    SOURCE_DIR ${PROJECT_BINARY_DIR}/../${cuda_devdriver_version}
    URL "http://${ION_UPDATE_SERVER}/updates/software/external/${cuda_devdriver_file}.tar.gz" # cmake doesn't download .run files
#    URL_MD5 ""
    TIMEOUT "3600"
    CONFIGURE_COMMAND ""
    BUILD_COMMAND ""
    INSTALL_COMMAND ""
)


if(ION_USE_CUDA)
    message(STATUS "BUILD with CUDA ${CUDA_VERSION}")
    add_definitions(-DION_COMPILE_CUDA)
    if (NOT ION_USE_SYSTEM_CUDA)

        set(CUDA_TOOLKIT_ROOT_DIR "${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}")
        set(CUDA_INCLUDE_DIRS "${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}/include")
        set(CUDA_NVCC_EXECUTABLE "${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}/bin/nvcc")
        set(CUDA_CUDART_LIBRARY "${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}/lib64/libcudart.so")
        set(CUDA_TOOLKIT_INCLUDE "${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}/include")
        set(CUDA_cublas_LIBRARY "${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}/lib64/libcublas.so")
        set(CUDA_cufft_LIBRARY "${PROJECT_BINARY_DIR}/../${cuda_toolkit_version}/lib64/libcufft.so")
        set(CUDA_VERBOSE_BUILD OFF)
        set(CUDA_64_BIT_DEVICE_CODE ON)
        include(${CMAKE_ROOT}/Modules/FindCUDA.cmake)

    else()
        find_package(CUDA REQUIRED)
    endif()
    include_directories(${CUDA_INCLUDE_DIRS})
    message(STATUS "CUDA_LIBRARIES: ${CUDA_LIBRARIES}")
    message(STATUS "CUDA_INCLUDE_DIRS: ${CUDA_INCLUDE_DIRS}")
endif()

if (NOT ION_USE_SYSTEM_FFTW)
  set(proj_name "fftw")
  set(proj_version "3.3")
  set(proj_name_version "${proj_name}-${proj_version}")
  ExternalProject_add(${proj_name}
    URL "http://${ION_UPDATE_SERVER}/updates/software/external/fftw-3.3.tar.gz"
#    URL_MD5 "0728ab3ec0ebb06631ab3b80a5c3d130"
    PREFIX ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
    SOURCE_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}
    BINARY_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}-build
    CONFIGURE_COMMAND ${PROJECT_BINARY_DIR}/../${proj_name_version}/configure
    --enable-static --with-pic
    --prefix=${PROJECT_BINARY_DIR}/../${proj_name_version}-install
  )
  include_directories("${PROJECT_BINARY_DIR}/../${proj_name_version}-install/include")
  set(ION_FFTW_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-install/lib/libfftw3.a")
else()
  set(ION_FFTW_LIBS libfftw3.a)
endif()

if(NOT ION_USE_SYSTEM_HDF5)
  set(proj_name "hdf5")
  set(proj_version "1.8.8")
  set(proj_name_version "${proj_name}-${proj_version}")
  ExternalProject_add(${proj_name}
    URL "http://${ION_UPDATE_SERVER}/updates/software/external/hdf5-1.8.8.tar.gz"
#    URL_MD5 1196e668f5592bfb50d1de162eb16cff
    PREFIX ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
    SOURCE_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}
    BINARY_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}-build
    CONFIGURE_COMMAND ${PROJECT_BINARY_DIR}/../${proj_name_version}/configure
    --with-pic=yes --disable-shared --with-pthread --enable-threadsafe
    --prefix=${PROJECT_BINARY_DIR}/../${proj_name_version}-install
  )
  include_directories("${PROJECT_BINARY_DIR}/../${proj_name_version}-install/include")
  set(ION_HDF5_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-install/lib/libhdf5.a")
  set(ION_HDF5_HL_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-install/lib/libhdf5_hl.a")
else()
  set(HDF5_USE_STATIC_LIBRARIES True)
  find_package(HDF5 REQUIRED)
  set(ION_HDF5_LIBS ${HDF5_LIBRARIES})
endif()

if(NOT ION_USE_SYSTEM_ARMADILLO)
  set(proj_name "armadillo")
  set(proj_version "3.800.2+ion0")
  set(proj_name_version "${proj_name}-${proj_version}")
  set(ARMADILLO_PATCH "${PROJECT_SOURCE_DIR}/../external/armadillo-3.800.2+ion0.patch")
  ExternalProject_add(${proj_name}_proj
    URL "http://${ION_UPDATE_SERVER}/updates/software/external/armadillo-3.800.2.tar.gz"
    #URL_MD5 e8400bc7adb65fa0edc8c8ccb49b60eb
    PREFIX ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
    SOURCE_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}
    BINARY_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}-build
    CMAKE_ARGS
      -DARMA_USE_WRAPPER:BOOL=FALSE
      -DARMA_USE_MKL:BOOL=${ION_USE_MKL}
      -DCMAKE_INSTALL_PREFIX:PATH=${PROJECT_BINARY_DIR}/../${proj_name_version}-install
    PATCH_COMMAND patch -p0 -t -N < "${ARMADILLO_PATCH}"
  )
  set(ION_ARMADILLO_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-install/lib/libarmadillo.so")
  include_directories("${PROJECT_BINARY_DIR}/../${proj_name_version}-install/include")
else()
  set(ION_ARMADILLO_LIBS "armadillo")
#include_directories("/home/csugnet/local/src/armadillo-3.0.2/include")
#set(ION_ARMADILLO_LIBS "/home/csugnet/local/src/armadillo-3.0.2/libarmadillo.so")
endif()


set(LIBKMEANSFILE "${PROJECT_SOURCE_DIR}/../external/kmeans-1.7/libkmeans.a")
add_custom_command(OUTPUT ${LIBKMEANSFILE}
    COMMAND "make"
    WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}/../external/kmeans-1.7")
add_library(LIBKMEANS STATIC IMPORTED)
set_property(TARGET LIBKMEANS PROPERTY IMPORTED_LOCATION ${LIBKMEANSFILE})
include_directories("${PROJECT_SOURCE_DIR}/../external/kmeans-1.7")

if(NOT ION_USE_SYSTEM_BOOST)
  set(proj_name "boost")
  set(proj_version "1.49.0")
  set(proj_name_version "${proj_name}-${proj_version}")
  ExternalProject_add(${proj_name}
    SVN_REPOSITORY "http://svn.boost.org/svn/boost/tags/release/Boost_1_49_0"
    PREFIX ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
    SOURCE_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}
    BINARY_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}-build
    UPDATE_COMMAND ""
    CONFIGURE_COMMAND ./bootstrap.sh
    BUILD_IN_SOURCE 1
    BUILD_COMMAND     ./bjam install
    --build-dir=${PROJECT_BINARY_DIR}/../${proj_name_version}-build
    --prefix=${PROJECT_BINARY_DIR}/../${proj_name_version}-install
    --with-serialization
    INSTALL_COMMAND ""
    )
  include_directories("${PROJECT_BINARY_DIR}/../${proj_name_version}-install/include")
  set(ION_BOOST_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-install/lib/libboost_serialization.a")
else()
  find_package(Boost REQUIRED)
  include_directories(${Boost_INCLUDE_DIRS})
  set(ION_BOOST_LIBS "boost_serialization.a")
endif()

if(NOT ION_USE_SYSTEM_UNWIND)
  set(proj_name "unwind")
  set(proj_version "1.0.1")
  set(proj_name_version "${proj_name}-${proj_version}")
  ExternalProject_add(${proj_name}
    URL "http://${ION_UPDATE_SERVER}/updates/software/external/libunwind-1.0.1.tar.gz"
    PREFIX ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
    SOURCE_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}
    BINARY_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}-build
    CONFIGURE_COMMAND ${PROJECT_BINARY_DIR}/../${proj_name_version}/configure
    CFLAGS=-D_FORTIFY_SOURCE=0
    --with-pic=yes --disable-shared
    --prefix=${PROJECT_BINARY_DIR}/../${proj_name_version}-install
  )
  include_directories("${PROJECT_BINARY_DIR}/../${proj_name_version}-install/include")
  set(ION_UNWIND_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-install/lib/libunwind.a")
else()
  set(UNWIND_USE_STATIC_LIBRARIES True)
  find_package(UNWIND REQUIRED)
  set(ION_UNWIND_LIBS ${UNWIND_LIBRARIES})
endif()

# uncomment for profiling if gprofile and unwind installed
# add_library(UNWIND STATIC IMPORTED)
# set_property(TARGET UNWIND PROPERTY IMPORTED_LOCATION "/usr/local/lib/libunwind.a")
# add_library(PROFILE STATIC IMPORTED)
# set_property(TARGET PROFILE PROPERTY IMPORTED_LOCATION "/usr/local/lib/libprofiler.a")

include_directories("${PROJECT_BINARY_DIR}")
include_directories("${PROJECT_SOURCE_DIR}")
include_directories("${PROJECT_SOURCE_DIR}/../external/jsoncpp-src-amalgated0.6.0-rc1")


if (NOT ION_USE_SYSTEM_SAMTOOLS)
  set(proj_name "samtools")
  set(proj_version "0.1.18+git20130201+ion3")
  set(proj_name_version "${proj_name}-${proj_version}")
  ExternalProject_add(${proj_name}
    URL "http://${ION_UPDATE_SERVER}/updates/software/external/samtools-0.1.18+git20130201.tar.gz"
    #URL_MD5 7373854ca343ebee6a52874fcfc3a4a4
    PREFIX ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
    SOURCE_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}
    BINARY_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}-build
    CMAKE_ARGS
       -DBUILD_SHARED_LIBS:BOOL=OFF
       -DCMAKE_INSTALL_PREFIX:PATH=${PROJECT_BINARY_DIR}/../${proj_name_version}-install
    PATCH_COMMAND patch -p0 -t -N < "${ION_TS_EXTERNAL}/samtools-0.1.18+git20130201+ion3.patch"
  )
  include_directories("${PROJECT_BINARY_DIR}/../${proj_name_version}-install/include")
  set(ION_SAMTOOLS_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-install/lib/libbam.a")
else()
    set(ION_SAMTOOLS_LIBS libbam.a)
    include_directories("/usr/include/samtools")
endif()


if (NOT ION_USE_SYSTEM_PICARD)
  set(proj_name "picard")
  set(proj_version "1.77")
  set(proj_name_version "${proj_name}-${proj_version}")
  ExternalProject_add(${proj_name}
    URL "http://updates.iontorrent.com/updates/software/external/picard-tools-1.77.tar.gz"
    #URL_MD5 
    # in source build
    PREFIX ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
    SOURCE_DIR "${PROJECT_BINARY_DIR}/../${proj_name_version}"
    BINARY_DIR "${PROJECT_BINARY_DIR}/../${proj_name_version}-build"
    CONFIGURE_COMMAND ""
    BUILD_COMMAND ""
    INSTALL_COMMAND ""
  )
endif()


if (NOT ION_USE_SYSTEM_BAMTOOLS)
    set(proj_name "bamtools")
    set(revision_tag efd4f7b683)#set date below accordingly
    set(proj_version "2.2.2.20121216+git${revision_tag}")
    set(proj_name_version "${proj_name}-${proj_version}")
    ExternalProject_add(${proj_name}
      URL "http://${ION_UPDATE_SERVER}/updates/software/external/bamtools-2.2.2.20121216+gitefd4f7b683.tar.gz"
#      GIT_REPOSITORY "https://github.com/iontorrent/bamtools.git"
#      GIT_TAG ${revision_tag}
      PREFIX ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
      SOURCE_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}
      BINARY_DIR ${PROJECT_BINARY_DIR}/../${proj_name_version}-build
      CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX:PATH=${PROJECT_BINARY_DIR}/../${proj_name_version}-install
    )
    include_directories("${PROJECT_BINARY_DIR}/../${proj_name_version}-install/include/bamtools")
    set(ION_BAMTOOLS_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-install/lib/bamtools/libbamtools.a")
else()
    set(ION_BAMTOOLS_LIBS libbamtools.a)
    include_directories("/usr/include/bamtools")
endif()


if(NOT ION_USE_SYSTEM_LEVMAR)
  set(proj_name "levmar")
  set(proj_version "2.6")
  set(proj_name_version "${proj_name}-${proj_version}")
  ExternalProject_add(${proj_name}
    #URL            "http://www.ics.forth.gr/~lourakis/levmar/levmar-2.6.tgz"
    URL             "http://updates.iontorrent.com/updates/software/external/levmar-2.6.tgz"
    PREFIX          ${PROJECT_BINARY_DIR}/../${proj_name_version}-prefix
    SOURCE_DIR      ${PROJECT_BINARY_DIR}/../${proj_name_version}
    BINARY_DIR      ${PROJECT_BINARY_DIR}/../${proj_name_version}-build
    CMAKE_ARGS      -DLINSOLVERS_RETAIN_MEMORY=0 -DHAVE_LAPACK=0 -DNEED_F2C=0 -DBUILD_DEMO=0
    INSTALL_COMMAND ""
  )
  include_directories("${PROJECT_BINARY_DIR}/../${proj_name_version}")
  set(ION_LEVMAR_LIBS "${PROJECT_BINARY_DIR}/../${proj_name_version}-build/liblevmar.a")
else()
  set(LEVMAR_USE_STATIC_LIBRARIES True)
  find_package(LEVMAR REQUIRED)
  set(ION_LEVMAR_LIBS ${LEVMAR_LIBRARIES})
endif()

